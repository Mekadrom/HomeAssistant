import org.springframework.boot.gradle.plugin.SpringBootPlugin

repositories {
    mavenLocal()
    mavenCentral()
}

subprojects { subproject ->
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: SpringBootPlugin
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.adarshr.test-logger'
    apply plugin: 'info.solidsoft.pitest'
    apply plugin: 'org.sonarqube'

    subproject.buildDir = file("${gradle.ext.distDir.path}/simulator/${subproject.name}/build")

    bootRun {
        systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active') ?: 'local'
        if (System.hasProperty('server.port')) {
            systemProperty 'server.port', System.getProperty('server.port')
        }
    }

    bootJar {
        dependsOn processResources
        archiveFileName = "${archiveBaseName.get()}.jar"
    }

    processResources {
        filesMatching('**/*.yml') {
            expand(subproject.properties)
            expand(ext.properties)
            expand(gradle.ext.properties)
        }
    }

    springBoot {
        buildInfo()
    }

    sourceSets {
        itest {
            java {
                srcDirs("src/itest")
            }
        }
    }

    clean {
        delete(files('build'))
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    task itest(type: Test) {
        testClassesDirs = sourceSets.itest.output.classesDirs
        classpath = sourceSets.itest.runtimeClasspath
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        dependsOn itest

        reports.xml.enabled = true
        reports.xml.destination file("${subproject.buildDir.path}/jacoco/test/jacocoXml")
        reports.html.destination file("${subproject.buildDir.path}/jacoco/test/jacocoHtml")

        executionData tasks.withType(Test).findAll { it.state.executed }
        sourceSets sourceSets.main
    }

    jacoco {
        toolVersion = "0.8.7"
    }

    pmd {
        ruleSets = []
        ruleSetFiles = files("${rootDir}/pmd/pmd-rules.xml")
    }

    pitest {
        testSourceSets = [sourceSets.test, sourceSets.itest]
        mainSourceSets = [sourceSets.main]
        junit5PluginVersion = '0.14'
        timestampedReports = false
        outputFormats = ['HTML']
        reportDir = file("${subproject.buildDir.path}/reports/pitest")
    }

    sonarqube {
        properties {
            property 'sonar.projectName', subproject.projectName
            property 'sonar.projectKey', subproject.projectKey
            property 'sonar.login', "${gradle.ext.simulator_sonar_login}"
            property 'sonar.sources', 'src/main'
            property 'sonar.tests', 'src/test'
            property 'sonar.java.binaries', "${gradle.ext.distDir.path}/simulator/${subproject.name}/build/classes"
            property 'sonar.java.test.binaries', "${gradle.ext.distDir.path}/simulator/${subproject.name}/build/classes"
            property 'sonar.jdbc.dialect', 'org.h2.Driver'
            property 'sonar.binaries', "${gradle.ext.distDir.path}/simulator/${subproject.name}/build/classes"
            property 'sonar.language', 'java'
            property 'sonar.sourceEncoding', 'UTF-8'
            property 'sonar.junit.reportsPath', "${gradle.ext.distDir.path}/simulator/${subproject.name}/build/test-results"
            property 'sonar.java.coveragePlugin', "jacoco"
            property 'sonar.coverage.jacoco.xmlReportPaths', "${gradle.ext.distDir.path}/simulator/${subproject.name}/build/jacoco/test/jacocoXml"
        }
    }
}

task test {
    dependsOn subprojects.collect { it.test }
}

task itest {
    dependsOn subprojects.collect { it.itest }
}

task clean {
    dependsOn subprojects.collect { it.clean }
    delete(files('build'))
}

task pmdMain {
    dependsOn subprojects.collect { it.pmdMain }
}

task pmdTest {
    dependsOn subprojects.collect { it.pmdTest }
}

task pmdItest {
    dependsOn subprojects.collect { it.pmdItest }
}

task pitest {
    // this one doesn't work the other way for some reason
    dependsOn subprojects.collect { ':simulator:' + it.name + ':pitest' }
}

task sonarqube {
    dependsOn subprojects.collect {':simulator:' + it.name + ':sonarqube' }
}

task build {
    dependsOn subprojects.collect { it.build }
}

task jar {
    dependsOn subprojects.collect { it.jar }
}
