import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'base'
}

repositories {
    mavenCentral()
}

subprojects {
//    apply plugin: 'application'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'
    apply plugin: SpringBootPlugin

    group = 'com.higgs'
    version = '1.0.0'

    sourceCompatibility = 16
    targetCompatibility = 16

    repositories {
        mavenCentral()
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'

        compileOnly 'org.projectlombok:lombok'

        compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

        implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

        implementation group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

        implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.kafka:spring-kafka'

        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.kafka:spring-kafka-test'
        testImplementation 'org.springframework.security:spring-security-test'

        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'

        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

task buildAll {
    dependsOn ':node_common:build'
    dependsOn ':node_backend:build'
    dependsOn ':server:build'
}

clean {
    delete 'dist'
}

task copyToDist(type: Copy) {
    dependsOn clean
    doFirst {
        mkdir 'dist'
    }

    from('node_common/build/libs') {
        include '**'
    }
    into 'dist'

    from('node_backend/build/libs') {
        exclude '**plain.jar'
    }
    into 'dist'

    from('server/build/libs') {
        exclude '**plain.jar'
    }
    into 'dist'
}

task jarAll {
    dependsOn 'buildAll'
    finalizedBy 'copyToDist'
}
