import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'java'
}

group = 'com.higgs'
version = '1.0.0'

ext {
    distDir = 'dist'
    runScriptsDir = 'runscripts'
}

repositories {
    mavenCentral()
}

subprojects { subproject ->
    group = 'com.higgs'
    version = '1.0.0'

    subproject.apply plugin: 'io.spring.dependency-management'
    subproject.apply plugin: 'java'
    subproject.apply plugin: SpringBootPlugin

    sourceCompatibility = 17
    targetCompatibility = 17

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        runtimeDependency {
            canBeResolved = true
        }
        testRuntimeDependency {
            canBeResolved = true
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        configurations.implementation.extendsFrom(configurations.runtimeDependency)
        configurations.testImplementation.extendsFrom(configurations.testRuntimeDependency)

        annotationProcessor 'org.projectlombok:lombok'

        runtimeDependency 'org.projectlombok:lombok'

        runtimeDependency group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

        runtimeDependency group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

        runtimeDependency group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'

        runtimeDependency group: 'org.springframework', name: 'spring-webmvc', version: '5.3.8'
        runtimeDependency 'org.springframework.boot:spring-boot-starter-oauth2-client'
        runtimeDependency 'org.springframework.boot:spring-boot-starter-security'
        runtimeDependency 'org.springframework.kafka:spring-kafka'
        runtimeDependency 'org.springframework.boot:spring-boot-starter-websocket'

        runtimeDependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

        testRuntimeDependency 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeDependency 'org.springframework.kafka:spring-kafka-test'
        testRuntimeDependency 'org.springframework.security:spring-security-test'

        testRuntimeDependency 'org.junit.jupiter:junit-jupiter-api:5.7.0'

        testRuntimeDependency 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    }

    test {
        useJUnitPlatform()
    }
}

task buildAll {
    dependsOn ':loginservice:build'
    dependsOn ':server:build'
}

clean.doFirst {
    delete "${distDir}"
}

task copyToDist(type: Copy) {
    dependsOn clean
    doFirst {
        mkdir "${distDir}"

        subprojects.stream().forEach {
            mkdir "${distDir}/${it.name}"
        }
    }

    into("${distDir}")

    subprojects.forEach {proj ->
        into("${proj.name}") {
            with copySpec {
                from("${proj.name}/build/libs") {
                    exclude '*-*'
                }
            }
        }
    }

    into('') {
        with copySpec {
            from("${runScriptsDir}")
        }
    }
}

task jarAll {
    dependsOn 'buildAll'
    finalizedBy 'copyToDist'
}
