import org.springframework.boot.gradle.plugin.SpringBootPlugin

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'io.spring.dependency-management'
apply plugin: SpringBootPlugin
apply plugin: 'pmd'
apply plugin: 'com.adarshr.test-logger'
apply plugin: 'info.solidsoft.pitest'
apply plugin: 'org.sonarqube'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.13.0'

    implementation group: 'org.springframework', name: 'spring-webmvc', version: '5.3.8'
    implementation group: 'org.springframework.kafka', name: 'spring-kafka', version: '2.7.6'

    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    implementation('org.springframework.boot:spring-boot-starter-web') {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }

    implementation group: 'com.higgs', name: 'common', version: version

    implementation group: 'org.reflections', name: 'reflections', version: '0.10.2'

    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.22'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'
}

sourceSets {
    itest {
        java {
            srcDirs("src/itest")
        }
    }
}

bootJar {
    dependsOn processResources
    archiveFileName = "${archiveBaseName.get()}.jar"
}

bootRun {
    jvmArgs = ["-Dorg.slf4j.simpleLogger.defaultLogLevel=debug"]
}

processResources {
    filesMatching('**/*.yml') {
        expand(project.properties)
        expand(ext.properties)
        expand(gradle.ext.properties)
    }
}

springBoot {
    buildInfo()
}

pmd {
    ruleSets = []
    ruleSetFiles = files("${rootDir}/pmd/pmd-rules.xml")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

pitest {
    testSourceSets = [sourceSets.test, sourceSets.itest]
    mainSourceSets = [sourceSets.main]
    junit5PluginVersion = '0.14'
    timestampedReports = false
    outputFormats = ['HTML']
}

task itest(type: Test) {
    testClassesDirs = sourceSets.itest.output.classesDirs
    classpath = sourceSets.itest.runtimeClasspath
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    dependsOn itest

    reports.xml.enabled = true
    reports.xml.destination file("build/jacoco/test/jacocoXml")
    reports.html.destination file("build/jacoco/test/jacocoHtml")

    executionData tasks.withType(Test).findAll { it.state.executed }
    sourceSets sourceSets.main
}

jacoco {
    toolVersion = "0.8.7"
}

sonarqube {
    properties {
        property 'sonar.projectName', 'Action Server'
        property 'sonar.projectKey', 'Action-Server'
        property 'sonar.sources', 'src/main'
        property 'sonar.tests', 'src/test'
        property 'sonar.java.binaries', 'build/classes'
        property 'sonar.java.test.binaries', 'build/classes'
        property 'sonar.jdbc.dialect', 'org.h2.Driver'
        property 'sonar.binaries', 'build/classes'
        property 'sonar.language', 'java'
        property 'sonar.sourceEncoding', 'UTF-8'
        property 'sonar.junit.reportsPath', 'build/test-results'
        property 'sonar.java.coveragePlugin', 'jacoco'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/jacoco/test/jacocoXml'
    }
}
